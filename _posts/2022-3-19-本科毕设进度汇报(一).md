---
layout: post
author: shopkeeper
categories: thesis gis
---

## 行政区搜索

先根据输入的行政区确定需要搜索的空间编码节点，再根据关键词搜索空间编码下的所有POI节点，并筛选排除掉不是目标行政区的POI

```sql
MATCH (r:`地名节点`{name:"洪山区"})<-[:`管辖`]-(:`地名节点`{name:"武汉市"})<-[:`管辖`]-(:`地名节点`{name:"湖北省"}) 
WITH r  
MATCH (s:`空间编码节点`)<-[:`包含`*]-(:`空间编码节点`)<-[:`覆盖`]-(r)  
WHERE NOT EXISTS((s)-[:`包含`]->())   
WITH DISTINCT s  
MATCH (p:`POI节点`)<-[:`拥有`]-(s)  
WHERE p.name CONTAINS "武汉大学" AND p.province = "湖北省"  AND p.city = "武汉市"  AND p.area = "洪山区"  
WITH p limit 500  
RETURN p SKIP 0 LIMIT 20    
```

![](/assets/images/2022-3-19-本科毕设进度汇报(一)-1.png)

## 矩形搜索

前端需要计算出矩形框对应的北斗网格位置码集合。后端先根据输入的北斗网格位置码集合，找到对应的空间编码节点，再根据关键词搜索空间编码下的所有POI节点。需要使用neo4j的spatial函数判断每一个POI节点是否在目标矩形框内。

```sql
MATCH (s1:`空间编码节点`)<-[:`包含`*0..]-(s2:`空间编码节点`)  
WHERE s2.name in ['N50H05151','N50H05152','N50H05161','N50H05162']  AND NOT EXISTS((s1)-[:`包含`]->())   
WITH DISTINCT s1, point({longitude: 114.34730756629993, latitude: 30.526945436494568}) AS southWest, point({longitude: 114.36080943321458, latitude: 30.533658653748606}) AS northEast   
MATCH (p:`POI节点`)<-[:`拥有`]-(s1)  WHERE p.name CONTAINS "武汉大学"  AND point.withinBBox(point({longitude: toFloat(p.wgs84_lng), latitude: toFloat(p.wgs84_lat)}), southWest, northEast)  
WITH p limit 500  
RETURN p SKIP 20 LIMIT 20
```

![](/assets/images/2022-3-19-本科毕设进度汇报(一)-2.png)

## 圆形搜索

前端需要计算出圆形对应的北斗网格位置码集合。后端先根据输入的北斗网格位置码集合，找到对应的空间编码节点，再根据关键词搜索空间编码下的所有POI节点。需要使用neo4j的spatial函数判断每一个POI节点到圆心的距离是否小于圆半径。

```sql
MATCH (s1:`空间编码节点`)<-[:`包含`*0..]-(s2:`空间编码节点`)  
WHERE s2.name in ['N50H05161','N50H05151']  AND NOT EXISTS((s1)-[:`包含`]->())   
WITH DISTINCT s1, point({longitude: 114.35170623801244, latitude: 30.531767695653233}) AS center  
MATCH (p:`POI节点`)<-[:`拥有`]-(s1)  
WHERE p.name CONTAINS "武汉大学"  AND point.distance(point({longitude: toFloat(p.wgs84_lng), latitude: toFloat(p.wgs84_lat)}),center) < 167.675  
 WITH p limit 500  
 RETURN p SKIP 20 LIMIT 20
```

![](/assets/images/2022-3-19-本科毕设进度汇报(一)-3.png)