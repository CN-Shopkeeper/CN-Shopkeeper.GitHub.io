---
layout: post
author: shopkeeper
categories: thesis gis
---

## 新的数据导入方式

### 最初的方式

- 预处理分别生成POI节点、空间编码节点、地名节点
- 分别单独导入上述三种节点
- 分别单独建立节点之间的关系

劣势：

- 文件要导入多次。实际上一次导入即可解决问题
- 建立节点关系时需要重新查找对应节点，时间复杂度高

### 优化1

- 预处理生成携带有空间编码信息、地名信息的POI节点
- 一次导入数据，直接创建POI节点，利用携带的空间编码信息、地名信息创建空间编码节点和地名节点
- 对每一行数据的POI节点、空间编码节点、地名节点，直接建立联系

参考CQL语句：

```sql
LOAD CSV WITH HEADERS FROM "file:///sdars/travel.csv" AS row
CREATE (pro:属性节点{name:row.name,type:"景点信息",star:row.star,score:row.score,price:row.score,sale:row.sale,intro:row.intro})
CREATE(p:POI节点 :文旅 {name: row.name, address: row.address, province: row.province, city: row.city, area:row.area, wgs84_lng:row.wgs84_lng, wgs84_lat:row.wgs84_lat, grid:row.grid, grid1:row.grid1,grid2:row.grid2,grid3:row.grid3,bdglc:row.bdglc, type:row.type})
CREATE (p)-[:`拥有属性`]->(pro)
WITH p
MERGE (b:`空间编码节点`{level:4,name:p.grid,type:"bdglcNode"})
MERGE (b1:`空间编码节点`{level:1,name:p.grid1,type:"bdglcNode"})
MERGE (b2:`空间编码节点`{level:2,name:p.grid2,type:"bdglcNode"})
MERGE (b3:`空间编码节点`{level:3,name:p.grid3,type:"bdglcNode"})
MERGE (b1)-[:包含]->(b2)
MERGE (b2)-[:包含]->(b3)
MERGE (b3)-[:包含]->(b)
MERGE (province:`地名节点`{name:p.province,level:"province",type:"areaNode"})
MERGE (city:`地名节点`{name:p.city,level:"city",type:"areaNode"})
MERGE (district:`地名节点`{name:p.area,level:"district",type:"areaNode"})
MERGE (province)-[:管辖]->(city)
MERGE (city)-[:管辖]->(district)
MERGE (province)-[:覆盖]->(b1)
MERGE (city)-[:覆盖]->(b2)
MERGE (district)-[:覆盖]->(b3)
CREATE (b)-[:拥有]->(p)
REMOVE p.grid
REMOVE p.grid1
REMOVE p.grid2
REMOVE p.grid3
```

劣势：

- 每一行数据操作复杂，且执行时间过长，当数据量很大时，容易爆内存。
- 每次添加新节点时都是创建新节点（`CREATE`），如果导入已经存在的POI节点或向同一个POI节点导入属性数据，会创建重复的节点；但是，如果只是简单的进行合并操作（`MERGE`），可能会覆盖掉节点的原有属性。

### 优化2：

- 预处理工作同`优化1`相同
- 导入时，首先使用`USING PERIODIC COMMIT`进行分批次导入，每次导入数量为1000行，防止内存不足
- 通过全部属性来合并`MERGE`POI节点，当新建`ON CREATE`时，说明该POI节点原先不存在，需要再为其建立空间编码节点、地名节点联系，因此对其一个标志位（p.grid），用于接下来进一步处理；如果是匹配到了`ON MATCH`，就说明已经和空间编码节点、地名节点关联过，不需要进一步处理。
- 由于导入的属性数据，部分字段可能为空，所以需要判断，当存在某一列字段不为空时，再进行导入。Cypher的条件判断需要通过循环来实现。当向本来为空的字段赋值时，需要使用`coalesce`函数来进行处理。
- 处理空间编码节点、地名节点时，需要判断之前是否设置了标志位（p.grid），如果设置了，说明需要进行导入空间编码节点、地名节点时

参考CQL语句：

```sql
:auto USING PERIODIC COMMIT
LOAD CSV WITH HEADERS FROM "file:///sdars/2022年4月1日.csv" AS row
MERGE(p:POI节点 :文旅 {name: row.name, address: row.address, province: row.province, city: row.city, area:row.area, wgs84_lng:row.wgs84_lng, wgs84_lat:row.wgs84_lat, bdglc:row.bdglc, type:row.type})
ON CREATE
SET p.grid=row.grid
FOREACH(ignoreMe IN CASE WHEN NOT row.intro IS NULL
    OR NOT row.open_time IS NULL
    OR NOT row.ctrip_link IS NULL
    OR NOT row.meituan_link IS NULL
    OR NOT row.qunar_link IS NULL
    OR NOT row.dianping_link IS NULL
    THEN [1] ELSE [] END |
    MERGE (pro:属性节点{name:row.name,type:"景点信息", prior:1})
    SET
      pro.intro = coalesce(row.intro,pro.intro),
      pro.open_time = coalesce(row.open_time,pro.open_time),
      pro.ctrip_link = coalesce(row.ctrip_link,pro.ctrip_link),
      pro.meituan_link = coalesce(row.meituan_link,pro.meituan_link),
      pro.qunar_link = coalesce(row.qunar_link,pro.qunar_link),
      pro.dianping_link = coalesce(row.dianping_link,pro.dianping_link)
    MERGE (p)-[:`拥有属性`]->(pro)
)
FOREACH(ignoreMe IN CASE WHEN NOT row.imagesStr IS NULL THEN [1] ELSE [] END |
    MERGE (pic:属性节点 {name: row.name, type: "图片", prior:21})
    SET pic.urls = coalesce(pic.urls,[])+split(row.imagesStr, ';')
    MERGE (p)-[:`拥有属性`]->(pic)
)
WITH p,row
FOREACH(ignoreMe IN CASE WHEN NOT p.grid IS NULL THEN [1] ELSE [] END |
    MERGE (b1:`空间编码节点`{level:1,name:row.grid1,type:"bdglcNode"})
    MERGE (b2:`空间编码节点`{level:2,name:row.grid2,type:"bdglcNode"})
    MERGE (b3:`空间编码节点`{level:3,name:row.grid3,type:"bdglcNode"})
    MERGE (b:`空间编码节点`{level:4,name:row.grid,type:"bdglcNode"})
    MERGE (b1)-[:包含]->(b2)
    MERGE (b2)-[:包含]->(b3)
    MERGE (b3)-[:包含]->(b)
    MERGE (province:`地名节点`{name:row.province,level:"province",type:"areaNode"})
    MERGE (city:`地名节点`{name:row.city,level:"city",type:"areaNode"})
    MERGE (district:`地名节点`{name:row.area,level:"district",type:"areaNode"})
    MERGE (province)-[:管辖]->(city)
    MERGE (city)-[:管辖]->(district)
    MERGE (province)-[:覆盖]->(b1)
    MERGE (city)-[:覆盖]->(b2)
    MERGE (district)-[:覆盖]->(b3)
    MERGE (b)-[:拥有]->(p)
    REMOVE p.grid
)
```

## 接下来的工作

- 解决wz师姐提出的系统存在的问题
- 实现智能提示

## 可以研究的方向

通过阅读文献资料，目前已经探索出部分待研究的方向

- 复合条件检索

  [1]李翔. 支持复合条件查询的位置信息检索系统的设计与实现[D].北京邮电大学,2021.DOI:10.26969/d.cnki.gbydu.2021.001389.

- 全文检索（检索POI属性的intro字段）

  [1]张绍琳,曹平.基于Lucene的标准全文检索技术研究与应用[J].航空标准化与质量,2019(05):53-56.DOI:10.13237/j.cnki.asq.2019.05.011.

- POI节点之间可达性、距离、最短路径计算（与本系统关系不大）

  [1]Manuel Hotz,Theodoros Chondrogiannis,Leonard Wörteler,Michael Grossniklaus. Experiences with Implementing Landmark Embedding in Neo4j[P]. Graph Data Management Experiences & Systems (GRADES) and Network Data Analytics (NDA),2019.